* THIS PROGRAM FORAMTS MINIMAL LISTING
           &ANCHOR = 1
* PATTERNS
           BLANK = SPAN(' ')
           EJECT  = BLANK 'ejc'
           COMMENT = '*' ( LEN(6) REM . CMT | NULL . CMT )
* COMMENT FLAG
           CMTFLG  = 0
*           INSTRUCTION = BLANK ( )
           TITLE  = BLANK 'ttl' LEN(2) ( REM . ENTRY )
           TITSEG = ( BREAK('-') . MAINTL ) '-- ' REM . SUBTL
           MAC    = (( '.' TAB(5) ) . LABE ) LEN(2) ( LEN(5) . MEM )
+                   SPAN(' ') REM . CMT
           CDMAC  = '.' ( 'if' | 'then' | 'else' | 'fi' ) . KEY
* MACRO NEST LEVEL COUNTER
           NESTC  = 0
           INSTR  = ( TAB(5) . LABE ) LEN(2) ( TAB(10) . MEM )
+                 ( LEN(2) ( TAB(28) . OPE ) (( NULL | BREAK(' ')) . PADD ) ' '
+                 ( REM . CMT ) | NULL )
           OPE = ''
           CMT = ''
* PRELUDE
           OUTPUT = '\input formatmac'

* NEW PAGE
NEWPG      OUTPUT = ''
* READ LOOP
READ       LINE = INPUT                       :F(FIN)

* MATCH CASES
           LINE  CDMAC                        :S(ISCMAC)
           LINE  MAC                          :S(ISMAC)
           LINE  COMMENT                      :S(ISCOMMENT)
           LINE  EJECT                        :S(ISEJC)
           LINE  TITLE                        :S(ISTIL)
           LINE  INSTR                        :S(ISINS)
           LINE  ''                           :S(READ)F(END)


ISCMAC     KEY 'if'                           :S(FMIF)
           TMP    = NESTC - 1
           OUTPUT = '\asmac{' TMP '}{' KEY '}'
           KEY 'fi'                           :F(READ)
           NESTC  = NESTC - 1
           OUTPUT = '\vskip6pt'               :(READ)
FMIF       LINE TAB(7) LEN(5) . SYM
           OUTPUT = '\ifmac{' NESTC '}{' SYM '}'
           NESTC  = NESTC + 1                 :(READ)
ISCOMMENT  EQ(CMTFLG,1)                       :S(CONTCM)
           OUTPUT = '\vskip6pt'
CONTCM     OUTPUT = '\lnie'
           OUTPUT = CMT '\endln'
           CMTFLG = 1                         :(READ)
ISEJC      OUTPUT = '\vfill\break'            :(NEWPG)
ISTIL      ENTRY TITSEG
           &ANCHOR = 0
MLOOP      MAINTL ' ' =                         :S(MLOOP)
           &ANCHOR = 1
           OUTPUT = '\vfill\break'
           OUTPUT = '\titl{' MAINTL '}{' SUBTL '}'      :(READ)
ISINS      LABE   = TRIM(LABE)
           OPE    = OPE PADD
           OPE    = TRIM(OPE)
           CMTFLG = 0
           OUTPUT = '\inst{' LABE '}{' MEM '}{' OPE '}{' CMT '}'
+                                                     :(READ)
ISMAC      CMTFLG = 0
           OUTPUT = '\dfmc{' LABE '}{' MEM '}{' CMT '}'
+                                                     :(READ)
FIN        OUTPUT = '\bye'
END
